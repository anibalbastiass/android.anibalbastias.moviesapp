plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
}

android {
    def CONFIG = { k -> "${credentialsProperties.get(k)}" }

    compileSdkVersion versions.android.compile

    defaultConfig {
        applicationId "com.anibalbastias.moviesapp"
        minSdkVersion versions.android.minSdk
        targetSdkVersion versions.android.targetSdk
        versionCode 5
        versionName "1.4.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "API_BASE_URL", CONFIG("API_BASE_URL")
        buildConfigField "String", "API_HOST", CONFIG("API_HOST")

        buildConfigField "String", "API_PUBLIC_KEY_HASH", CONFIG("API_PUBLIC_KEY_HASH")
        buildConfigField "String", "API_PUBLIC_KEY_HASH_AWS1", CONFIG("API_PUBLIC_KEY_HASH_AWS1")
        buildConfigField "String", "API_PUBLIC_KEY_HASH_AWS2", CONFIG("API_PUBLIC_KEY_HASH_AWS2")
        buildConfigField "String", "API_PUBLIC_KEY_HASH_AWS3", CONFIG("API_PUBLIC_KEY_HASH_AWS3")

        buildConfigField "String", "API_KEY", CONFIG("API_KEY")
        buildConfigField "String", "IMAGE_URL", CONFIG("IMAGE_URL")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion versions.deps.compose
        kotlinCompilerVersion versions.deps.kotlin
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api project(path: lib_modules.uikit_compose)

    // Kotlin and Coroutines
    implementation deps.jetbrains.kotlinStdlib
    implementation deps.jetbrains.kotlinCoroutinesCore
    implementation deps.jetbrains.kotlinCoroutinesAndroid

    // Android X
    implementation deps.androidx.appCompat
    implementation deps.androidx.cardView
    implementation deps.androidx.constraintLayout
    implementation deps.androidx.constraintLayoutSolver
    implementation deps.androidx.fragmentKtx
    implementation deps.androidx.lifecycle.liveData
    implementation deps.androidx.lifecycle.viewModel
    implementation deps.androidx.lifecycle.runtimeKtx
    implementation deps.androidx.navigationFragment
    implementation deps.androidx.recyclerView
    implementation deps.androidx.swipeRefreshLayout
    implementation deps.androidx.core.splashScreen

    // Room
    implementation deps.androidx.room.runtime
    implementation deps.androidx.room.ktx
    implementation deps.androidx.room.paging
    kapt deps.androidx.room.compiler

    // Paging
    implementation deps.androidx.paging.runtime
    implementation deps.androidx.paging.compose

    // Compose
    implementation deps.androidx.compose.ui
    implementation deps.androidx.compose.material
    implementation deps.androidx.compose.activity
    implementation deps.androidx.compose.navigation
    implementation deps.androidx.compose.constraintLayout
    implementation deps.androidx.compose.accompanist.swipeRefresh
    implementation deps.androidx.compose.accompanist.navigationAnimation
    implementation deps.androidx.compose.accompanist.flowLayout
    implementation deps.androidx.compose.uiPreview
    debugImplementation deps.androidx.compose.uiPreviewDebug

    // Google
    implementation deps.google.material
    implementation deps.google.flexbox

    // Square
    implementation deps.square.retrofit
    implementation deps.square.okhttpLogging
    implementation deps.square.moshi.core
    implementation deps.square.moshi.converter
    kapt deps.square.moshi.codeGen

    // 3rd party libs
    implementation deps.thirdParty.coilCompose
    implementation deps.thirdParty.lottieCompose
    implementation deps.thirdParty.sharedElementCompose
    implementation deps.thirdParty.toolbarCompose

    // Dagger Hilt
    implementation deps.google.daggerHilt.android
    implementation deps.androidx.daggerHilt.viewModel
    implementation deps.androidx.daggerHilt.navigationCompose

    // Dagger Hilt Compiler
    kapt deps.google.daggerHilt.compiler
    kapt deps.androidx.daggerHilt.compiler

    // Coroutines tests
    testImplementation deps.test.androidx.coreTesting
    testImplementation deps.test.jetbrains.kotlinCoroutines

    // Testing-only dependencies
    debugImplementation deps.test.androidx.fragmentTesting
    debugImplementation deps.test.androidx.fragmentKtx
    debugImplementation deps.test.androidx.testRules

    // Unit and UI Test
    testImplementation deps.test.junit
    testImplementation deps.test.mockito
    testImplementation deps.test.robolectric
    testImplementation deps.test.androidx.testCore
    testImplementation deps.test.androidx.testExtJUnit
    testImplementation deps.test.androidx.espressoCore
    testImplementation deps.test.androidx.espressoIntents
    testImplementation deps.test.androidx.exthTruth
    testImplementation deps.test.mockWebServer
    testImplementation deps.square.moshi.core

    // UI and Instrumented tests
    androidTestImplementation deps.test.androidx.espressoCore
    androidTestImplementation deps.test.androidx.coreTesting
    androidTestImplementation deps.test.androidx.testExtJUnit
    androidTestImplementation deps.instrumented.android.roomTesting
    androidTestImplementation deps.instrumented.androidx.navigationTesting
    androidTestImplementation deps.instrumented.androidx.testCore
    androidTestImplementation deps.instrumented.androidx.testCoreKtx
    androidTestImplementation deps.instrumented.androidx.testExtJUnitKtx
    androidTestImplementation deps.instrumented.androidx.testRunner
}